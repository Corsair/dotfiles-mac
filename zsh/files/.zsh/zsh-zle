# -*- mode: sh; -*-

bindkey -e
# Use $EDITOR to edit command line...
autoload -U   edit-command-line
zle -N        edit-command-line
# Automagically escape crap in URL when needed.
autoload -U url-quote-magic
zle -N self-insert url-quote-magic

# Quickly input `../'
rationalise-dot() {
    if [[ $LBUFFER = *.. ]]; then
        LBUFFER+=/..
    else
        LBUFFER+=.
    fi
}
zle -N rationalise-dot

# If return on empty line, execute `ls'.
user-ret()
{
    if [[ $BUFFER = "" ]] ;then
        BUFFER="ls"
        zle end-of-line
        zle accept-line
    else
        zle accept-line
    fi
}
zle -N user-ret

go-up()
{
    pushd ..
    zle reset-prompt
    echo
}
zle -N go-up

# Custom Key Bindings
bindkey "\e;" vi-pound-insert
bindkey . rationalise-dot

# Fix Home and End key
# rxvt
bindkey "\e[8~" end-of-line
bindkey "\e[7~" beginning-of-line

bindkey "\e[1~" beginning-of-line
bindkey "\e[4~" end-of-line
# # for xterm
# bindkey "\eOH" beginning-of-line
# bindkey "\eOF" end-of-line

bindkey "\r" user-ret
bindkey '\ee' edit-command-line
bindkey '\e6' go-up
